plugins {
	// see settings.gradle for fabric-loom version
	id 'fabric-loom'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName  = "${project.archives_base_name}-fabric-${fabric_minecraft_version}"
}

repositories {
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		url "https://maven.terraformersmc.com/"
		content {
			includeGroup "com.terraformersmc"
		}
	}
	maven {
		url "https://maven.shedaniel.me/"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.fabric_minecraft_version}"
	mappings "net.fabricmc:yarn:${project.fabric_yarn_version}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	// Cloth Config - https://maven.shedaniel.me/me/shedaniel/cloth/cloth-config-fabric/
	modApi("me.shedaniel.cloth:cloth-config-fabric:${dependency_cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	//Mod Menu - https://github.com/TerraformersMC/ModMenu/wiki/API
	modImplementation "com.terraformersmc:modmenu:${dependency_mod_menu_version}"
}

processResources {
	filesMatching("fabric.mod.json") {
		expand([
				"version": project.version,
				"depends_fabric_loader": "${fabric_loader_version}",
				"depends_minecraft": "${fabric_minecraft_version}",
				"depends_cloth_config": "${dependency_cloth_config_version}"
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
